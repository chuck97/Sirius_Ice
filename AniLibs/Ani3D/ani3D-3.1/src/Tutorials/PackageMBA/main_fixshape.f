c ==========================================================
      Program  main
c ==========================================================
c This program loads a mesh (mesh.out) generated by 
c the alibaft3D library (using the AFT format), fix it by
c improving shape quality, and saves back in AFT format
c (mesh.out) and in GMV format (mesh.gmv).
c
c If mesh.out is not present in the current directory, the program
c downloads ../data/aft.out and saves the fixed mesh to save.out
c
c The program uses libraries libmba3D.a and libview3D.a
c ==========================================================
      implicit none

      integer nvmax,ntmax,nbmax
c ... nvmax - maximum number of mesh nodes
c ... ntmax - maximum number of mesh tetrahedra
c ... nbmax - maximum number of boundary faces
      parameter(nvmax = 550 000, ntmax = 6*nvmax, nbmax = 300 000)

c ... work memory
      Integer   MaxWr, MaxWi
      Parameter(MaxWr = 20 000 000, MaxWi = 90 000 000)

      Integer  iW(MaxWi)
      Real*8   rW(MaxWr)



c ... standard mesh arrays (see doc/user_guide.pdf for more detail)
c ... number of points, tets, and boundary faces
      Integer  nv, nt, nb 

c ... coordinates of mesh points 
      Real*8   vrt(3,nvmax)

c ... connectivity table for tets and their labels
      Integer  tet(4,ntmax),material(ntmax)

c ... connectivity table for boundary faces, and their labels
      Integer  bnd(3,nbmax),labelF(nbmax)

c ... additional mesh arrays are needed to call mesh_metric() from libmba3D.a 
c     ivfix(nvfix) is array of fixed (never touched) points
c     ibfix(nbfix) is array of fixed (never touched) faces
c     itfix(ntfix) is array of fixed (never touched) elements
      Integer  nvfix, ivfix(nvmax), nbfix, ibfix(nbmax), ntfix, itfix(1)

c ... library MBA
c Basket capacity and the maximum number of local modifications
      Integer   MaxSkipE, MaxQItr
      Parameter(MaxSkipE = 3000, MaxQItr = 500 000)

c Desired final mesh quality
      Real*8    Quality
      Parameter(Quality = 4D-1)

      Real*8    rQuality
      Logical   flagAuto
      Integer   iPrint, status, iERR

c The routine which defines the nodal metric to be Euclidean
      Integer   ANI_Metric_Eucl
      External  ANI_Metric_Eucl

      Integer i,minmat,minlab
c ======================================================================
c Load the input mesh (output from aniAFT)

      Call loadMaft( nvmax, nbmax, ntmax,  nv, nb, nt,
     &     vrt, bnd, tet, labelF, material,
     &     nvfix, nbfix, ntfix, ivfix, ibfix, itfix,
     &     iW, iW, "mesh.out")
c    &     iW, iW, "../data/nozzle/nozzle-1.out")


      Write(*,*)
      Write(*,'(A,I6,A)') 'The loaded mesh has ',nt,' tetrahedra'

c Find the minimal colors of elements and boundary faces
      minmat = 100000
      do i=1,nt
         minmat = min(minmat,material(i))
      end do
      minlab = 100000
      do i=1,nb
         minlab = min(minlab,labelF(i))
      end do

c Shift materials to positive values
      if (minmat.le.0) then
         do i=1,nt
            material(i)=material(i)-minmat+1
         end do
      end if
      if (minlab.le.0) then
         do i=1,nb
            labelF(i)=labelF(i)-minlab+1
         end do
      end if

c mesh fixed points (not to be touched in adaptation) may be detected automatically since
c they separate boundary edges with different colors
      nvfix = 0 
      nbfix = 0
      ntfix = 0

c generate a shape regular mesh with nt tetrahedra.
c Shape regularity is understood in the metric defined in subroutine
c ANI_Metric_Eucl (see below).
      flagAuto = .TRUE.  ! default mesh generation options
      status = 0+16      ! forbid boundary elements + freeze surface points
      iPrint = 2         ! average level of output information


      Call mbaFixShape(
c group (M)
     &     nv, nvmax, nb, nbmax, nt, ntmax,
     &     vrt, bnd, tet, labelF, material,
c group (Dev)
     &     nvfix, nbfix, ntfix, ivfix, ibfix, itfix,
     &     flagAuto, status,
c group (Q)
     &     MaxSkipE, MaxQItr,
     &     ANI_Metric_Eucl, Quality, rQuality,
c group (W)
     &     MaxWr, MaxWi, rW, iW,
     &     iPrint, iERR)

c Shift materials back to original values
      if (minmat.le.0) then
         do i=1,nt
            material(i)=material(i)+minmat-1
         end do
      end if
      if (minlab.le.0) then
         do i=1,nb
            labelF(i)=labelF(i)+minlab-1
         end do
      end if


c Save the fixed mesh in AFT format and
c create a GMV-file with the mesh. The name must have extension .gmv

      Call saveMaft(nv, nb, nt, vrt, bnd, tet, labelF, material,
     &             "mesh.out")
c    &             "save.out")
      Call saveMgmv(nv, nb, nt, vrt, bnd, tet, labelF, material, 
     &             "mesh.gmv", iW)
c    &             "save.gmv", iW)

      Write(*,*)
      Write(*,'(A,I6,A)') 'The saved mesh has ',nt,' tetrahedra'

      Stop
      End


      
